
Процедура ПодпискаНаСобытиеПриЗаписи(Источник, Отказ) Экспорт

	Если Источник.ОбменДанными.Загрузка = Истина Тогда		
		Возврат;
	КонецЕсли;  
	
	НовоеСообщение = Справочники.ИсходящиеСообщенияRMQ.СоздатьЭлемент();

	НовоеСообщение.КлючМаршрутизации = "waybills";
	
	Сериализатор = Новый СериализаторXDTO(ФабрикаXDTO);
	
	Запись = Новый ЗаписьJSON;
	Запись.УстановитьСтроку();
	Сериализатор.ЗаписатьJSON(Запись, Источник, НазначениеТипаXML.Явное);
	
	НовоеСообщение.ТекстСообщения = Запись.Закрыть();
	
	НовоеСообщение.Записать();
	
КонецПроцедуры

Процедура ОтправкаСообщенийRMQ() Экспорт
	
	СтруктураПодключения = Новый Структура("Адрес, Порт, Логин, Пароль, ВиртуальныйХост", "localhost", 5672, "guest", "guest", "otus"); 
	ТочкаОбмена = "mdm";
			
	КлиентКомпоненты = Неопределено;
	Если Не ИнициализироватьКомпонентуКлиентСервер(КлиентКомпоненты) Тогда
		
		ПодключитьКомпонентуСервер();
		ИнициализироватьКомпонентуКлиентСервер(КлиентКомпоненты);
		
	КонецЕсли;
	
	Если ПроверитьПодключение(КлиентКомпоненты, СтруктураПодключения) Тогда		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ИсходящиеСообщенияRMQ.Ссылка КАК Ссылка,
			|	ИсходящиеСообщенияRMQ.Код КАК Код,
			|	ИсходящиеСообщенияRMQ.КлючМаршрутизации КАК КлючМаршрутизации,
			|	ИсходящиеСообщенияRMQ.ТекстСообщения КАК ТекстСообщения
			|ИЗ
			|	Справочник.ИсходящиеСообщенияRMQ КАК ИсходящиеСообщенияRMQ
			|ГДЕ
			|	ИсходящиеСообщенияRMQ.Отправлено = ЛОЖЬ";
		
		РезультатЗапроса = Запрос.Выполнить();
		
		Выборка = РезультатЗапроса.Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Если ОтправитьСообщение(КлиентКомпоненты, СтруктураПодключения, ТочкаОбмена, Выборка.КлючМаршрутизации, Выборка.ТекстСообщения) Тогда
				Сообщение = Выборка.Ссылка.ПолучитьОбъект();
				Сообщение.Отправлено = Истина;
				Сообщение.Записать();
			КонецЕсли;
		КонецЦикла;	
	КонецЕсли;
	
КонецПроцедуры   

Процедура ПолучениеСообщенийRMQ() Экспорт
	
	СтруктураПодключения = Новый Структура("Адрес, Порт, Логин, Пароль, ВиртуальныйХост", "localhost", 5672, "guest", "guest", "otus"); 
	ТочкаОбмена = "mdm";
			
	КлиентКомпоненты = Неопределено;
	Если Не ИнициализироватьКомпонентуКлиентСервер(КлиентКомпоненты) Тогда
		
		ПодключитьКомпонентуСервер();
		ИнициализироватьКомпонентуКлиентСервер(КлиентКомпоненты);
		
	КонецЕсли;        
	
	МассивПолучателей = Новый Массив;
	МассивПолучателей.Добавить("waybills");

	Для Каждого ИмяОчереди Из МассивПолучателей Цикл
		Если ПроверитьПодключение(КлиентКомпоненты, СтруктураПодключения) Тогда	 
			Сериализатор = Новый СериализаторXDTO(ФабрикаXDTO);

			ОтветноеСообщение = ПрочитатьСообщение(КлиентКомпоненты, СтруктураПодключения, ИмяОчереди);
			Пока ОтветноеСообщение <> "" Цикл  
				ЧтениеJSON = Новый ЧтениеJSON;
				ЧтениеJSON.УстановитьСтроку(ОтветноеСообщение); 
				Попытка
					ЗначениеОбъект = Сериализатор.ПрочитатьJSON(ЧтениеJSON);
					ЧтениеJSON.Закрыть();  
					Сообщить(ЗначениеОбъект);
				Исключение 
					СистемнаяОшибка = ОписаниеОшибки();
					ТекстСообщения = "Ошибка чтения сообщения!%СистемнаяОшибка%";
					ТекстСообщения = СтрЗаменить(ТекстСообщения, "%СистемнаяОшибка%", СистемнаяОшибка);
					Сообщить(ТекстСообщения);
				КонецПопытки;
				
				ОтветноеСообщение = ПрочитатьСообщение(КлиентКомпоненты, СтруктураПодключения, ИмяОчереди);
			КонецЦикла;
		КонецЕсли; 
	КонецЦикла;
	
КонецПроцедуры    

Функция ПрочитатьСообщение(КлиентКомпоненты, СтруктураПодключения, ИмяОчереди)
	
	ОтветноеСообщение = "";
	
	Попытка
		КлиентКомпоненты.Connect(
			СтруктураПодключения.Адрес,
			СтруктураПодключения.Порт,
			СтруктураПодключения.Логин,
			СтруктураПодключения.Пароль,
			СтруктураПодключения.ВиртуальныйХост);
		
		ИмяОчереди = ИмяОчереди;
		
		Попытка
			КлиентКомпоненты.DeclareQueue(ИмяОчереди, Ложь, Ложь, Ложь, Ложь);
			
			Потребитель = КлиентКомпоненты.BasicConsume(ИмяОчереди, "", Истина, Ложь, 0);
			
			ОтветноеСообщение = "";
			Если КлиентКомпоненты.BasicConsumeMessage("", ОтветноеСообщение, 5) Тогда
				КлиентКомпоненты.BasicAck();
				//Форма.ОтветноеСообщение = ОтветноеСообщение;
				ТекстСообщения = НСтр("ru='Сообщение успешно прочитано!'");
			Иначе
				//Форма.ОтветноеСообщение = ОтветноеСообщение;
				ТекстСообщения = НСтр("ru='Очередь пустая!'");
			КонецЕсли;
			Сообщить(ТекстСообщения);
			
			КлиентКомпоненты.BasicCancel("");
		Исключение
			ВызватьИсключение КлиентКомпоненты.GetLastError();
		КонецПопытки;
	Исключение
		СистемнаяОшибка = ОписаниеОшибки();
		ТекстСообщения = "Ошибка чтения сообщения!%СистемнаяОшибка%";
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%СистемнаяОшибка%", СистемнаяОшибка);
		ВызватьИсключение ТекстСообщения;
	КонецПопытки; 
	
	Возврат ОтветноеСообщение;
	
КонецФункции

Функция ОтправитьСообщение(КлиентКомпоненты, СтруктураПодключения, ТочкаОбмена, ИмяОчереди, ТекстСообщения)
	
	Попытка
		КлиентКомпоненты.Connect(
			СтруктураПодключения.Адрес,
			СтруктураПодключения.Порт,
			СтруктураПодключения.Логин,
			СтруктураПодключения.Пароль,
			СтруктураПодключения.ВиртуальныйХост);
		
		КлиентКомпоненты.BasicPublish(
			ТочкаОбмена,
			ИмяОчереди,
			ТекстСообщения,
			1,
			Ложь);
	Исключение
		СистемнаяОшибка = ОписаниеОшибки();
		ТекстСообщения = "Ошибка отправки сообщения!%СистемнаяОшибка%";
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%СистемнаяОшибка%", СистемнаяОшибка);
		ВызватьИсключение ТекстСообщения;
	КонецПопытки;
	
	Сообщить("Сообщение успешно отправлено!");  
	Возврат Истина;
	
КонецФункции

Функция ИнициализироватьКомпонентуКлиентСервер(Компонента)
	
	Попытка
		Компонента  = Новый("AddIn.BITERP.PinkRabbitMQ");
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

Функция ПроверитьПодключение(КлиентКомпоненты, Форма)
	
	Попытка
		КлиентКомпоненты.Connect(
				Форма.Адрес,
				Форма.Порт,
				Форма.Логин,
				Форма.Пароль,
				Форма.ВиртуальныйХост);
			Исключение
		СистемнаяОшибка = ОписаниеОшибки();
		ТекстСообщения = "Ошибка подключения!%СистемнаяОшибка%";
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%СистемнаяОшибка%", СистемнаяОшибка);
		ВызватьИсключение ТекстСообщения;
	КонецПопытки;   
	
	Возврат Истина;

КонецФункции   

Функция ПолучитьАдресМакетаКомпановки()
	
	МакетВнешнейКомпоненты    = ПолучитьОбщийМакет("PinkRabbitMQWin");
	АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(МакетВнешнейКомпоненты);
	
	Возврат АдресВоВременномХранилище;
	
КонецФункции

Процедура ПодключитьКомпонентуСервер(КомпонентаПодключена = Неопределено)
	
	АдресВоВременномХранилище = ПолучитьАдресМакетаКомпановки();
	КомпонентаПодключена = ПодключитьВнешнююКомпоненту(
			АдресВоВременномХранилище,
			"BITERP",
			ТипВнешнейКомпоненты.Native);
	
КонецПроцедуры
 