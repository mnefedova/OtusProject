
Процедура ОбработкаПроведения(Отказ, Режим)
	
	НачалоЗамера = ОценкаПроизводительности.НачатьЗамерВремени();
		
	// регистр ПоказанияОдометра
	Движения.ПоказанияОдометра.Записывать = Истина; 
	Движения.ОстаткиТопливаВБаке.Записывать = Истина;
	Движения.ДвижениеТоплива.Записывать = Истина;
	Движения.ПробегТранспортногоСредства.Записывать = Истина;
		
	Движения.ОстаткиТопливаВБаке.Записать();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ОстаткиТопливаВБаке");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("ТранспортноеСредство", ТранспортноеСредство);
	
	Блокировка.Заблокировать(); 
		
	// Списывать будем бензин с остатков
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СУММА(ПутевойЛистМаршрут.Расход) КАК Количество
		|ИЗ
		|	Документ.ПутевойЛист.Маршрут КАК ПутевойЛистМаршрут
		|ГДЕ
		|	ПутевойЛистМаршрут.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ОстаткиТопливаВБакеОстатки.Номенклатура КАК Номенклатура,
		|	СУММА(ОстаткиТопливаВБакеОстатки.КоличествоОстаток) КАК КоличествоОстаток
		|ПОМЕСТИТЬ втОстатки
		|ИЗ
		|	РегистрНакопления.ОстаткиТопливаВБаке.Остатки(&Дата, ТранспортноеСредство = &ТранспортноеСредство) КАК ОстаткиТопливаВБакеОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ОстаткиТопливаВБакеОстатки.Номенклатура
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ПутевойЛистТопливо.Номенклатура,
		|	СУММА(ПутевойЛистТопливо.Количество)
		|ИЗ
		|	Документ.ПутевойЛист.Топливо КАК ПутевойЛистТопливо
		|ГДЕ
		|	ПутевойЛистТопливо.Ссылка = &Ссылка
		|
		|СГРУППИРОВАТЬ ПО
		|	ПутевойЛистТопливо.Номенклатура
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втОстатки.Номенклатура КАК Номенклатура,
		|	СУММА(втОстатки.КоличествоОстаток) КАК КоличествоОстаток
		|ИЗ
		|	втОстатки КАК втОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	втОстатки.Номенклатура
		|ИТОГИ
		|	СУММА(КоличествоОстаток)
		|ПО
		|	ОБЩИЕ";
	
	Запрос.УстановитьПараметр("Дата", Новый Граница(Дата, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ТранспортноеСредство", ТранспортноеСредство);
	Запрос.УстановитьПараметр("Ссылка", Ссылка);

	РезультатЗапроса = Запрос.ВыполнитьПакет();  
	
	ВыборкаКоличествоВДокументе = РезультатЗапроса[0].Выбрать();
	
	Если ВыборкаКоличествоВДокументе.Следующий() Тогда
	
		КоличествоВДокументе = ВыборкаКоличествоВДокументе.Количество;	
		
		ВыборкаОбщийИтог = РезультатЗапроса[2].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		ВыборкаОбщийИтог.Следующий();		// Общий итог
		
		Если ВыборкаОбщийИтог.КоличествоОстаток < КоличествоВДокументе Тогда
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не хватает " + Строка(КоличествоВДокументе - ВыборкаОбщийИтог.КоличествоОстаток) + " единиц топлива для транспортного средства " + ТранспортноеСредство + ". Проведение невозможно.";
			Сообщение.Сообщить();
			Отказ = Истина;
			
		КонецЕсли; 
		
		Если Не Отказ Тогда

			ВыборкаДетальныеЗаписи = ВыборкаОбщийИтог.Выбрать();
			
			КоличествоКСписанию = КоличествоВДокументе; 
			Пока ВыборкаДетальныеЗаписи.Следующий() И КоличествоКСписанию > 0 Цикл   				
				Количество = Мин(КоличествоКСписанию, ВыборкаДетальныеЗаписи.КоличествоОстаток);
				
				Движение = Движения.ОстаткиТопливаВБаке.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
				Движение.Период = Дата;
				Движение.ТранспортноеСредство = ТранспортноеСредство;   
				Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
				Движение.Количество = Количество; 
				
				КоличествоКСписанию = КоличествоКСписанию - Движение.Количество;
				
				Движение = Движения.ДвижениеТоплива.Добавить();
				Движение.Период = Дата;
				Движение.Организация = Организация;
				Движение.ТранспортноеСредство = ТранспортноеСредство; 
				Движение.Номенклатура = ВыборкаДетальныеЗаписи.Номенклатура;
				Движение.Количество = -Количество;  				
			КонецЦикла;
			
			Для Каждого ТекСтрокаМаршрут Из Маршрут Цикл
				Движение = Движения.ПоказанияОдометра.Добавить();
				Движение.Период = Дата;
				Движение.ТранспортноеСредство = ТранспортноеСредство;
				Движение.ОдометрНачало = ТекСтрокаМаршрут.ОдометрНачало;
				Движение.ОдометрКонец = ТекСтрокаМаршрут.ОдометрКонец;    
				
				Движение = Движения.ПробегТранспортногоСредства.Добавить();
				Движение.Период = Дата;
				Движение.ТранспортноеСредство = ТранспортноеСредство;
				Движение.Расстояние = ТекСтрокаМаршрут.Расстояние;
				Движение.Расход = ТекСтрокаМаршрут.Расход;
				Движение.РасходНорма = ТекСтрокаМаршрут.РасходНорма;
			КонецЦикла;

			Для Каждого ТекСтрокаТопливо Из Топливо Цикл
				Движение = Движения.ОстаткиТопливаВБаке.Добавить();
				Движение.ВидДвижения = ВидДвиженияНакопления.Приход;
				Движение.Период = Дата;
				Движение.ТранспортноеСредство = ТранспортноеСредство;
				Движение.Номенклатура = ТекСтрокаТопливо.Номенклатура;
				Движение.Количество = ТекСтрокаТопливо.Количество; 
				
				Движение = Движения.ДвижениеТоплива.Добавить();
				Движение.Период = Дата;
				Движение.Организация = Организация;
				Движение.ТранспортноеСредство = ТранспортноеСредство;
				Движение.Номенклатура = ТекСтрокаТопливо.Номенклатура;
				Движение.Количество = ТекСтрокаТопливо.Количество;
			КонецЦикла;
			
			
			Движения.РасчетыССотрудниками.Записывать = Истина;
			Движение = Движения.РасчетыССотрудниками.Добавить();
			Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
			Движение.Период = Дата;
			Движение.Сотрудник = Сотрудник;
			Движение.Сумма = СуммаДокумента;

		КонецЕсли; 
	КонецЕсли; 
	
	ОценкаПроизводительности.ЗакончитьЗамерВремени("ПроведениеДокументаПутевойЛист", НачалоЗамера);

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)

	СуммаДокумента = Топливо.Итог("Сумма");

КонецПроцедуры
