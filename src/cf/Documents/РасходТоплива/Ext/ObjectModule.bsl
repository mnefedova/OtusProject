//////////////////////////////////////////////////////////////////////////////// 
// ПРОЦЕДУРЫ И ФУНКЦИИ
//

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ОБЪЕКТА

Процедура ОбработкаПроведения(Отказ, Режим)

	// регистр ОстаткиТопливаВБаке Расход
	Движения.ОстаткиТопливаВБаке.Записывать = Истина;  
	Если Режим = РежимПроведенияДокумента.Оперативный Тогда
		Движения.ОстаткиТопливаВБаке.БлокироватьДляИзменения = Истина;
	КонецЕсли;	
	
	Для Каждого ТекСтрокаТовары Из Товары Цикл
		Движение = Движения.ОстаткиТопливаВБаке.Добавить();
		Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
		Движение.Период = Дата;
		Движение.ТранспортноеСредство = ТекСтрокаТовары.ТранспортноеСредство;
		Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
		Движение.Количество = ТекСтрокаТовары.Количество;
	КонецЦикла;	

	// регистр ДвижениеТоплива 
	Движения.ДвижениеТоплива.Записывать = Истина;
	Для Каждого ТекСтрокаТовары Из Товары Цикл
		Движение = Движения.ДвижениеТоплива.Добавить();
		Движение.Период = Дата;
		Движение.Организация = Организация;
		Движение.ТранспортноеСредство = ТекСтрокаТовары.ТранспортноеСредство;    
		Движение.Номенклатура = ТекСтрокаТовары.Номенклатура;
		Движение.Количество = -ТекСтрокаТовары.Количество;
	КонецЦикла; 
	
	Движения.Записать();
	
	//Контроль остатков при оперативном проведении
	Если Режим = РежимПроведенияДокумента.Оперативный Тогда
		// Создадим запрос, чтобы контролировать остатки по товарам
		Запрос = Новый Запрос("ВЫБРАТЬ
		                      |	СУММА(РасходТопливаТовары.Количество) КАК Количество,
		                      |	МАКСИМУМ(РасходТопливаТовары.НомерСтроки) КАК НомерСтроки,
		                      |	РасходТопливаТовары.ТранспортноеСредство КАК ТранспортноеСредство,
		                      |	РасходТопливаТовары.Номенклатура КАК Номенклатура
		                      |ПОМЕСТИТЬ втТовары
		                      |ИЗ
		                      |	Документ.РасходТоплива.Товары КАК РасходТопливаТовары
		                      |ГДЕ
		                      |	РасходТопливаТовары.Ссылка = &Ссылка
		                      |
		                      |СГРУППИРОВАТЬ ПО
		                      |	РасходТопливаТовары.ТранспортноеСредство,
		                      |	РасходТопливаТовары.Номенклатура
		                      |;
		                      |
		                      |////////////////////////////////////////////////////////////////////////////////
		                      |ВЫБРАТЬ
		                      |	втТовары.Номенклатура КАК Номенклатура,
		                      |	втТовары.ТранспортноеСредство КАК ТранспортноеСредство,
		                      |	втТовары.НомерСтроки КАК НомерСтроки,
		                      |	втТовары.Количество КАК Количество,
		                      |	ПРЕДСТАВЛЕНИЕ(втТовары.Номенклатура) КАК НоменклатураПредставление,
		                      |	-ЕСТЬNULL(ОстаткиТопливаВБакеОстатки.КоличествоОстаток, 0) КАК Нехватка
		                      |ИЗ
		                      |	РегистрНакопления.ОстаткиТопливаВБаке.Остатки КАК ОстаткиТопливаВБакеОстатки
		                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ втТовары КАК втТовары
		                      |		ПО ОстаткиТопливаВБакеОстатки.ТранспортноеСредство = втТовары.ТранспортноеСредство
		                      |			И ОстаткиТопливаВБакеОстатки.Номенклатура = втТовары.Номенклатура
		                      |ГДЕ
		                      |	ОстаткиТопливаВБакеОстатки.КоличествоОстаток < 0");

		Запрос.УстановитьПараметр("Ссылка", Ссылка);
		РезультатСНехваткой = Запрос.Выполнить();

		ВыборкаРезультатаСНехваткой = РезультатСНехваткой.Выбрать();

		// Выдадим ошибки для строк, в которых не хватает остатка
		Пока ВыборкаРезультатаСНехваткой.Следующий() Цикл

			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = НСтр("ru = 'Не хватает '", "ru") 
				+ ВыборкаРезультатаСНехваткой.Нехватка 
				+ НСтр("ru = ' единиц товара '", "ru") + """" 
				+ ВыборкаРезультатаСНехваткой.НоменклатураПредставление 
				+ """.";
			Сообщение.Поле = НСтр("ru = 'Товары'", "ru") 
				+ "[" 
				+ (ВыборкаРезультатаСНехваткой.НомерСтроки - 1) 
				+ "]." 
				+ НСтр("ru = 'Количество'", "ru");
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
			Отказ = Истина;

		КонецЦикла;

	КонецЕсли;	

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)


КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	

КонецПроцедуры

